plugins {
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'de.radammuc'
version = '0.0.2-SNAPSHOT'

sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    implementation 'redis.clients:jedis:3.7.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('npm_install', Exec) {
    group 'build'

    workingDir "${projectDir}/poll-app"

    commandLine 'npm', 'install'
}

tasks.register('npm_build', Exec) {
    group 'build'
    dependsOn npm_install

    workingDir "${projectDir}/poll-app"

    commandLine 'npm', 'run', 'build'
}

tasks.register('deleteReactAppFolder', Delete) {
    group 'build'
    dependsOn npm_build

    delete "${projectDir}/src/main/resources/react-app"
}

tasks.register('copyReactApp', Copy) {
    group 'build'
    dependsOn deleteReactAppFolder

    def buildDirNpm = "${projectDir}/poll-app/build"
    def resourcesPublic = "${projectDir}/src/main/resources/public"

    from buildDirNpm
    into resourcesPublic
}

tasks.register('cleanReactApp') {
    group 'build'
    delete "${projectDir}/poll-app/node_modules"
    delete "${projectDir}/poll-app/build"
    delete "${projectDir}/src/main/resources/public"
}

tasks.named('classes') {
    // TODO this would fix the 'bootJar problem' but would also always force a react build for other tasks
    // dependsOn copyReactApp
}
